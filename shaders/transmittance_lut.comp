#version 450

#extension GL_GOOGLE_include_directive : require

#include "bindings.glsl"
#include "atmosphere_common.glsl"
#include "lut_compute_common.glsl"

layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = BINDING_ATMO_OUTPUT_LUT, rgba16f) writeonly uniform image2D transmittanceLUT;

layout(binding = BINDING_ATMO_TRANSMITTANCE) uniform AtmosphereUniforms {
    AtmosphereParams params;
    vec4 sunDirection;
    vec4 cameraPosition;
    float padding[2];
} uAtmosphere;

const int TRANSMITTANCE_STEPS = 40;

// Compute transmittance along ray from (r, mu) to atmosphere boundary
vec3 ComputeTransmittance(float r, float mu) {
    float distance = DistanceToAtmosphereBoundary(r, mu, uAtmosphere.params.atmosphereRadius);
    vec3 opticalDepth = integrateOpticalDepth(r, mu, distance, TRANSMITTANCE_STEPS, uAtmosphere.params);
    return exp(-opticalDepth);
}

void main() {
    ivec2 texel = ivec2(gl_GlobalInvocationID.xy);
    ivec2 lutSize = imageSize(transmittanceLUT);

    if (isOutOfBounds(texel, lutSize)) {
        return;
    }

    vec2 uv = texelToUV(texel, lutSize);

    // Decode altitude and view zenith angle from UV
    float r, mu;
    UVToTransmittanceLUTParams(uv, r, mu, uAtmosphere.params);

    // Compute and store transmittance
    vec3 transmittance = ComputeTransmittance(r, mu);
    imageStore(transmittanceLUT, texel, vec4(transmittance, 1.0));
}
